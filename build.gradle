allprojects {
  group = 'iox.semirings.fhir'
  version = '0.0.1'
  repositories { mavenLocal(); mavenCentral() }
}

def libProjects = subprojects.findAll { it.name != 'd4m.acc.microservice' }

configure(libProjects) {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'

  java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
    withSourcesJar()
  }

  tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation', '-Xlint:none']
  }

  tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging { events "started", "passed", "failed", "skipped"; showStandardStreams = true }
  }

  tasks.named('sourcesJar', Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
      }
    }
  }
}
